FUNCTION_BLOCK "FB_SVD_MVD_Interface"
TITLE =%version: 0.07.1.1 % CN: 30
//Function:
//This function combined (OR function) the commands coming from the SCADA and the 
//operator panel for the SVZ/MVZ blocks
//The status given to operator panel are also made in this function
//
//History:
//Original version  %created_by:  nlhdl %
//                  %date_created: Monday, April 11, 2016 10:53:03 AM %
//                  %release: VI_CONV/RA_Maint_28.1 %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#12789: At Flank Detect SVD_MVD_Interface command   nlHdL    11-04-16   0.07.1.1
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.07
//24150: comment change udt MVD / SVD                   nlHdL    03-06-15   0.07
//21030: Markcode change SVZ MVZ to SVD MVD             nlHdL    24-09-14   0.06
//15797: Keyword release added                          nlwwij   26-09-13   0.05
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.04
//CR19512 Update sources according S7 Manager           nlHdL    7-3-13   0.03
//CR10260 lifesign bargraph added in HMI                nlple    19-06-12   0.02
//PR6738 initial version                                nlple    04-04-11   0.01
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_CMD_SCADA_Auto : BOOL ;	//CMD: For SCADA Auto
  i_CMD_SCADA_Manual : BOOL ;	//CMD: For SCADA Manual
  i_CMD_SCADA_Fixed_Up : BOOL ;	//CMD: For SCADA Fixed up
  i_CMD_SCADA_Fixed_Down : BOOL ;	//CMD: For SCADA Fixed down
  i_CMD_SCADA_Manual_Up : BOOL ;	//CMD: For SCADA Manual Up
  i_CMD_SCADA_Manual_Down : BOOL ;	//CMD: For SCADA Manual Down
  i_CMD_SCADA_ManTRS1Start : BOOL ;	//CMD: For SCADA manual TRS1 start
  i_CMD_SCADA_ManTRS1Stop : BOOL ;	//CMD: For SCADA manual TRS1 stop
  i_CMD_SCADA_ManTRS2Start : BOOL ;	//CMD: For SCADA manual TRS2 start
  i_CMD_SCADA_ManTRS2Stop : BOOL ;	//CMD: For SCADA manual TRS2 stop
  i_CMD_SCADA_ManTRS3Start : BOOL ;	//CMD: For SCADA manual TRS3 start
  i_CMD_SCADA_ManTRS3Stop : BOOL ;	//CMD: For SCADA manual TRS3 stop
  i_CMD_OP_Auto : BOOL ;	//CMD: For OP Auto
  i_CMD_OP_Manual : BOOL ;	//CMD: For OP Manual
  i_CMD_OP_Fixed_Up : BOOL ;	//CMD: For OP Fixed up
  i_CMD_OP_Fixed_Down : BOOL ;	//CMD: For OP Fixed down
  i_CMD_OP_Manual_Up : BOOL ;	//CMD: For OP Manual Up
  i_CMD_OP_Manual_Down : BOOL ;	//CMD: For OP Manual Down
  i_CMD_OP_ManTRS1Start : BOOL ;	//CMD: For OP manual TRS1 start
  i_CMD_OP_ManTRS1Stop : BOOL ;	//CMD: For OP manual TRS1 stop
  i_CMD_OP_ManTRS2Start : BOOL ;	//CMD: For OP manual TRS2 start
  i_CMD_OP_ManTRS2Stop : BOOL ;	//CMD: For OP manual TRS2 stop
  i_CMD_OP_ManTRS3Start : BOOL ;	//CMD: For OP manual TRS3 start
  i_CMD_OP_ManTRS3Stop : BOOL ;	//CMD: For OP manual TRS3 stop
  i_ST_Manual : BOOL ;	//ST: Mode Disabled (Offline or Manual)
  i_ST_Fixed_Up : BOOL ;	//ST: Fixed up mode activated
  i_ST_Fixed_Down : BOOL ;	//ST: Fixed down mode activated
  i_ST_Up : BOOL ;	//ST: SVD/MVD in Up (High) position
  i_ST_Down : BOOL ;	//ST: SVD/MVD in Down (Low) position  
END_VAR
VAR_OUTPUT
  o_Command : "UDT_SVD_MVD_Cmd";	//Command to SVD/MVZ block
  o_OP_Status : "UDT_SVD_MVD_Status";	//Status to OP
END_VAR
VAR
  s_Alive_Sign : INT ;	//Alive sign bar value
  s_FP_Command_Auto : BOOL ;	//Flank Positive Command Auto
  s_FP_Command_Manual : BOOL ;	//Flank Positive Command Manual
  s_FP_Command_Fixed_Up : BOOL ;	//Flank Positive Command Fixed Up
  s_FP_Command_Fixed_Down : BOOL ;	//Flank Positive Command Fixed Down
END_VAR
VAR_TEMP
  t_Cmd_SCADA : "UDT_SVD_MVD_Cmd";	
  t_Cmd_OP : "UDT_SVD_MVD_Cmd";	
END_VAR
BEGIN
NETWORK
TITLE =AA: Commands From SCADA or OP to SVD/MVD

      A     #i_CMD_SCADA_Auto; 
      O     #i_CMD_OP_Auto; 
      FP    #s_FP_Command_Auto; 
      =     #o_Command.Auto; 

      A     #i_CMD_SCADA_Manual; 
      O     #i_CMD_OP_Manual; 
      FP    #s_FP_Command_Manual; 
      =     #o_Command.Manual; 

      A     #i_CMD_SCADA_Fixed_Up; 
      O     #i_CMD_OP_Fixed_Up; 
      FP    #s_FP_Command_Fixed_Up; 
      =     #o_Command.Fixed_Up; 

      A     #i_CMD_SCADA_Fixed_Down; 
      O     #i_CMD_OP_Fixed_Down; 
      FP    #s_FP_Command_Fixed_Down; 
      =     #o_Command.Fixed_Down; 

      A     #i_CMD_SCADA_Manual_Up; 
      O     #i_CMD_OP_Manual_Up; 
      A     #i_ST_Manual; 
      =     #o_Command.Manual_Up; 

      A     #i_CMD_SCADA_Manual_Down; 
      O     #i_CMD_OP_Manual_Down; 
      A     #i_ST_Manual; 
      =     #o_Command.Manual_Down; 

      A     #i_CMD_SCADA_ManTRS1Start; 
      O     #i_CMD_OP_ManTRS1Start; 
      A     #i_ST_Manual; 
      =     #o_Command.Manual_TRS1_Start; 

      A     #i_CMD_SCADA_ManTRS1Stop; 
      O     #i_CMD_OP_ManTRS1Stop; 
      =     #o_Command.Manual_TRS1_Stop; 

      A     #i_CMD_SCADA_ManTRS2Start; 
      O     #i_CMD_OP_ManTRS2Start; 
      A     #i_ST_Manual; 
      =     #o_Command.Manual_TRS2_Start; 

      A     #i_CMD_SCADA_ManTRS2Stop; 
      O     #i_CMD_OP_ManTRS2Stop; 
      =     #o_Command.Manual_TRS2_Stop; 

      A     #i_CMD_SCADA_ManTRS3Start; 
      O     #i_CMD_OP_ManTRS3Start; 
      A     #i_ST_Manual; 
      =     #o_Command.Manual_TRS3_Start; 

      A     #i_CMD_SCADA_ManTRS3Stop; 
      O     #i_CMD_OP_ManTRS3Stop; 
      =     #o_Command.Manual_TRS3_Stop; 

NETWORK
TITLE =BA: Status to operator panel

      A     #i_ST_Manual; 
      =     #o_OP_Status.Manual; 

      A     #i_ST_Fixed_Up; 
      =     #o_OP_Status.Fixed_Up; 

      A     #i_ST_Fixed_Down; 
      =     #o_OP_Status.Fixed_Down; 

      A     #i_ST_Up; 
      =     #o_OP_Status.Pos_SWS_Up; 

      A     #i_ST_Down; 
      =     #o_OP_Status.Pos_SWS_Down; 


NETWORK
TITLE =CA: Bargraph lifesign HMI

      A     "M_OS_/1.0_sec."; // Increment every second the value for the alive sign bar.
      JCN   CA99; 
      L     #s_Alive_Sign; 
      +     1; 
      T     #s_Alive_Sign; 
      L     3; // Alive_Sign value [0..3]
      <=I   ; 
      JC    CA99; 
      L     0; 
      T     #s_Alive_Sign; 
CA99: NOP   0; 

      L     #s_Alive_Sign; 
      T     #o_OP_Status.Communication_Alive_Sign; 

END_FUNCTION_BLOCK

