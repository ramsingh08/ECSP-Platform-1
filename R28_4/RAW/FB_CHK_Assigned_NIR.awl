FUNCTION_BLOCK "FB_CHK_Assigned_NIR"
TITLE =%version: 0.04 % CN: 40
//Function:
//Check the LNK number in a DB_NIR_List, validate the LNK number in the 
//DB_NIR_List if they are not same then clear the LNK number in the DB_NIR_List.
//
//History:                                                                        
//This version   %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:35:10 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.04
//15797: Keyword release added                          nlwwij   26-09-13   0.04
//CR19512 Update sources according S7 Manager           nlHdL    8-3-13   0.03
//Renewed Check assigned block                          nlLvdG   22-03-12  0.02
//Initial revision                                      nlLvdG   21-03-12  0.01
FAMILY : General
VERSION : 0.0


VAR
  s_LNK_Number : INT ;	//Number of LNK which is checked
  s_List_assigned : BOOL ;	//Temp to check if list is assigned without DB PLT
  s_NIR_Number : INT ;	//NIR counter
END_VAR
VAR_TEMP
  t_Record_Length : INT ;	//Temp length of used record
  t_NIR_LNK_Found : INT ;	//Temp entry in NIR
  t_AR1_BackUp : DWORD ;	//AR1 back-up
  t_LNK_DB_NIR_Number : INT ;	//DB_LNK_List contains NIR number
END_VAR
BEGIN
NETWORK
TITLE =AA: Check the NIR List

      OPN   "DB_NIR_List"; //Open the NIR list
      L     DBW    2; 
      T     #t_Record_Length; 
      L     DBW    6; 
      +     -1; 
      L     #s_NIR_Number; //Checking for Maximum NIR Item present
      ==I   ; 
      JCN   AA02; 

      L     1; 
      T     #s_NIR_Number; 
      JU    AA99; 

AA02: L     #s_NIR_Number; //Incremental NIR number to DB_NIR_List
      +     1; 
      T     #s_NIR_Number; 

AA99: NOP   0; 

NETWORK
TITLE =BA: Check if NIR is Assigned
//If entry is not assigned then end block.
      L     #t_Record_Length; 
      L     #s_NIR_Number; 
      *D    ; 
      L     DBW    0; // Length header
      +D    ; 
      SLD   3; 
      LAR1  ; // Pointer to LNK entry in DB_LNK

// Loop to check if entry = 0
      L     DBW [AR1,P#0.0]; 
      T     #t_NIR_LNK_Found; //LNK Number in DB_NIR_List
      L     0; 
      <>I   ; 
      JCN   BA99; // NIR is not Assigned jump to end of the network

      TAR1  #t_AR1_BackUp; 

//Open the LNK_DB & verify the LIC

      OPN   "DB_LNK_List"; // Open LNK list
      L     DBW    2; 
      T     #t_Record_Length; 

      L     #t_NIR_LNK_Found; 
      L     #t_Record_Length; 
      *D    ; 
      L     DBW    0; // Length header
      +D    ; 
      SLD   3; 
      LAR1  ; 

      L     DBW [AR1,P#6.0]; 
      T     #t_LNK_DB_NIR_Number; //NIR assigned in the DB_LNK_List 


      L     #s_NIR_Number; //NIR to be checked
      L     #t_LNK_DB_NIR_Number; //NIR assigned in the DB_LNK_List 
      <>I   ; 
      JCN   BA99; //If Not equall clear the LNK in DB_NIR_List

      LAR1  #t_AR1_BackUp; 

      OPN   "DB_NIR_List"; 
      L     0; 
      T     DBW [AR1,P#0.0]; //LNK Number in DB_NIR_List

//-----Log the Event--------------//
// Log event will be reported by the FB_CHK_Assigned_LNK when NIR is cleared

BA99: NOP   0; 
END_FUNCTION_BLOCK

