FUNCTION "FC_Local_Mode_Dir" : VOID
TITLE =%version: 0.05 % CN: 33
//Function:
//Retreive data for instance DB and check the preffered destination with the
// fixed mode for direction 1, and for direction 2. 
//
//This version  %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:33:35 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.05
//15797: Keyword release added                          nlwwij   26-09-13   0.05
//PR11918: Adopted Decision point update with Dir A & B nlHdL    02-04-13   0.04
//CR19512 Update sources according S7 Manager           nlHdL    08-03-13   0.03
//PR11727 Add synergy key words 'created_by' and date   nlHdL    14-11-12   0.02
//Initial Revision                                      nlHdL    03-10-11   0.01
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_Fixed_Mode_Dir1 : BOOL ;	//The hardware is fixed in direction 1
  i_Fixed_Mode_Dir2 : BOOL ;	//The hardware is fixed in direction 2
  i_ANY_Preferred_Dir : ANY ;	//ANY pointer to the preferred direction
END_VAR
VAR_TEMP
  t_Destination : INT ;	//Destination
  t_Preference_A : BYTE ;	//Preference for Direction A
  t_Preference_B : BYTE ;	//Preference for Direction B
  t_DecisionLvl : BYTE ;	//Decision Level	
  t_FailedReason_A : BYTE ;	//Failed Reason for Direction A	
  t_FailedReason_B : BYTE ;	//Failed Reason for Direction B
  t_Direction : BYTE ;	//Direction selected
  t_DINO : INT ;	//DB number of instance	
  t_Succses_Dir : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =AA: Retreive data of destination x from FB

      L     P##i_ANY_Preferred_Dir; 
      LAR1  ; 

      L     W [AR1,P#2.0]; 
      L     6; 
      >I    ; 
      =     #t_Succses_Dir; 

      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     DIW [AR2,P#0.0]; 
      T     #t_Destination; 

      L     DIB [AR2,P#2.0]; 
      T     #t_Preference_A; 

      L     DIB [AR2,P#3.0]; 
      T     #t_Preference_B; 

      L     DIB [AR2,P#4.0]; 
      T     #t_DecisionLvl; 

      L     DIB [AR2,P#5.0]; 
      T     #t_FailedReason_A; 

      L     DIB [AR2,P#6.0]; 
      T     #t_FailedReason_B; 

      L     DIB [AR2,P#7.0]; 
      T     #t_Direction; 

      L     DINO; 
      T     #t_DINO; 
NETWORK
TITLE =BA: Check if Local Mode with out preferred direction

      AN    #i_Fixed_Mode_Dir1; 
      AN    #i_Fixed_Mode_Dir2; 
      BEC   ; 
//No local mode end block

      L     #t_Direction; 
      L     0; 
      <>I   ; 
      BEC   ; 

//Goto check local mode for the routing destination

      A     #i_Fixed_Mode_Dir1; 
      JCN   BA01; 
// If equipment is in fixed dir 1 mode (Local mode)

      L     1; 
      T     #t_Direction; 
      T     #t_Preference_A; 

      L     0; 
      T     #t_FailedReason_A; 

//Decision Level = 6 ("Local mode")
      L     6; 
      T     #t_DecisionLvl; 

      JU    BA99; 
//End fixed mode dir 1

BA01: A     #i_Fixed_Mode_Dir2; 
      JCN   BA99; 
// If equipment is in fixed dir 2 mode (Local mode)

      L     2; 
      T     #t_Direction; 
      T     #t_Preference_B; 

      L     0; 
      T     #t_FailedReason_B; 

//Decision Level = 6 ("Local mode")
      L     6; 
      T     #t_DecisionLvl; 

//End fixed mode dir 2

BA99: NOP   0; 
NETWORK
TITLE =CA: Write data to preferred direction

      L     P##i_ANY_Preferred_Dir; 
      LAR1  ; 

      OPN   DI [#t_DINO]; 
      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     #t_Destination; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 
      L     #t_Direction; 
      T     DIB [AR2,P#7.0]; 

END_FUNCTION

