FUNCTION "FC_Writ_RoutingEntry" : VOID
TITLE =%version: 0.02 %  CN: 30
//Function:
//Transfer routing information into routing table
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:29:26 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.02
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.02
//Initial Revision                                      nlHdL    29-03-13   0.01
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_DB_Route_Num : INT ;	//Number of DB routing table
  i_Destination : INT ;	//[1..300] Destination number
  i_Direction_And_Pref_1 : WORD ;	//Direction with Prefference, 0 = no dir., 1 = dir 1, 2 = dir 2, LB = pref, 1 = P
  i_Direction_And_Pref_2 : WORD ;	//Direction with Prefference, 0 = no dir., 1 = dir 1, 2 = dir 2, LB = pref, 1 = P
END_VAR
VAR_TEMP
  t_Route_DB_Nr : INT ;	//DB number of routing table
  t_ANY_Pointer : DWORD ;	//ANY pointer to the Destination
  t_Direction_1 : BYTE ;	//Direction 1
  t_Preference_1 : BYTE ;	//Preference 1
  t_Direction_2 : BYTE ;	//Direction 2
  t_Preference_2 : BYTE ;	//Preference 2
  t_Copy_Direction_1 : BOOL ;	//Copy of Direction 1 after check
  t_Copy_Direction_2 : BOOL ;	//Copy of Direction 2 after check
END_VAR
BEGIN
NETWORK
TITLE =AA: Split Direction_And_Preference into Direction and Preference

      L     #i_Direction_And_Pref_1; 
      SRW   8; 
      T     #t_Direction_1; 

      L     #i_Direction_And_Pref_1; 
      L     L#255; 
      AW    ; 
      T     #t_Preference_1; 

      L     #i_Direction_And_Pref_2; 
      SRW   8; 
      T     #t_Direction_2; 

      L     #i_Direction_And_Pref_2; 
      L     L#255; 
      AW    ; 
      T     #t_Preference_2; 
NETWORK
TITLE =BA: Check Destination values
// 
      NOP   0; 
// i_Destination must be more than 1 and less then or equal to 300
      L     1; 
      L     #i_Destination; 
      >I    ; 
      BEC   ; 

      L     300; 
      L     #i_Destination; 
      <I    ; 
      BEC   ; 

NETWORK
TITLE =CA:Check directions
//Direction 0 is not allowed!
// t_Direction_1 must be direction 1
      L     1; 
      L     #t_Direction_1; 
      ==I   ; 
      =     #t_Copy_Direction_1; 

// t_Direction_2 must be direction 2
      L     2; 
      L     #t_Direction_2; 
      ==I   ; 
      =     #t_Copy_Direction_2; 

NETWORK
TITLE =DA:Check preferences
//check #t_Preference_1 for validate value (value 0, 1, 2, 7 or 8)
      AN(   ; 

      O(    ; 
      L     #t_Preference_1; 
      L     0; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_1; 
      L     1; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_1; 
      L     2; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_1; 
      L     7; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_1; 
      L     8; 
      ==I   ; 
      )     ; 
      )     ; 
      BEC   ; 

//check #t_Preference_2 for validate value (value 0, 1, 2, 7 or 8)
      AN(   ; 

      O(    ; 
      L     #t_Preference_2; 
      L     0; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_2; 
      L     1; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_2; 
      L     2; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_2; 
      L     7; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #t_Preference_2; 
      L     8; 
      ==I   ; 
      )     ; 
      )     ; 
      BEC   ; 
NETWORK
TITLE =EA: Make pointer to correct table type within routing entry

      L     #i_DB_Route_Num; 
      T     #t_Route_DB_Nr; 
      OPN   DB [#t_Route_DB_Nr]; // Open Routing DB

// Calculate pointer to destionation entry
      L     #i_Destination; // Load destination number
      L     1; 
      -I    ; 
      JMZ   EA01; 
      L     2; // Each destination entry is 2 bytes long
      *D    ; 

EA01: SLD   3; // Conversion to pointer format
      L     P#DBX 0.0; // Pointer to the start of the open DB
      +D    ; 
      T     #t_ANY_Pointer; 

      LAR1  ; 

NETWORK
TITLE =FA: Copy direction 1 and direction 2

      A     #t_Copy_Direction_1; 
      JCN   FA01; 

      L     #t_Preference_1; 
      T     B [AR1,P#0.0]; 

FA01: A     #t_Copy_Direction_2; 
      JCN   FA02; 
      L     #t_Preference_2; 
      T     B [AR1,P#1.0]; 

FA02: NOP   0; 
END_FUNCTION

