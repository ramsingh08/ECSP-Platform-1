FUNCTION "FC_Check_Pref_Dest_x" : VOID
TITLE =%version: 0.05 % CN: 33
//Function:
//Retreive data for instance DB and then check the preffered destination
//
//This version  %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:33:02 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.05
//15797: Keyword release added                          nlwwij   26-09-13   0.05
//PR11918: Adopted Decision point update with Dir A & B nlHdL    02-04-13   0.04
//CR19512 Update sources according S7 Manager           nlHdL    08-03-13   0.03
//PR11727 Add synergy key words 'created_by' and date   nlHdL    14-11-12   0.02
//Initial Revision                                      nlHdL    28-09-11   0.01
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_ANY_Direction_A : ANY ;	//ANY pointer to the Direction A messages
  i_ANY_Direction_B : ANY ;	//ANY pointer to the Direction B messages
  i_ANY_Preferred_Dir : ANY ;	//ANY pointer to the preferred direction
END_VAR
VAR_TEMP
  t_Direction : BYTE ;	//Direction selected
  t_DINO : INT ;	//DB number of instance	
  t_Dir_A : STRUCT 	
   t_Destination : INT ;	//Destination
   t_Preference_A : BYTE ;	//Preference for Direction A
   t_Preference_B : BYTE ;	//Preference for Direction B
   t_DecisionLvl : BYTE ;	//Decision Level	
   t_FailedReason_A : BYTE ;	//Failed Reason for Direction A	
   t_FailedReason_B : BYTE ;	//Failed Reason for Direction B
  END_STRUCT ;	
  t_Dir_B : STRUCT 	
   t_Destination : INT ;	//Destination
   t_Preference_A : BYTE ;	//Preference for Direction A
   t_Preference_B : BYTE ;	//Preference for Direction B
   t_DecisionLvl : BYTE ;	//Decision Level	
   t_FailedReason_A : BYTE ;	//Failed Reason for Direction A	
   t_FailedReason_B : BYTE ;	//Failed Reason for Direction B
  END_STRUCT ;	
END_VAR
BEGIN
NETWORK
TITLE =AA: Retreive data of destination x from FB

      L     P##i_ANY_Direction_A; 
      LAR1  ; 

      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     DIW [AR2,P#0.0]; 
      T     #t_Dir_A.t_Destination; 

      L     DIB [AR2,P#2.0]; 
      T     #t_Dir_A.t_Preference_A; 

      L     DIB [AR2,P#3.0]; 
      T     #t_Dir_A.t_Preference_B; 

      L     DIB [AR2,P#4.0]; 
      T     #t_Dir_A.t_DecisionLvl; 

      L     DIB [AR2,P#5.0]; 
      T     #t_Dir_A.t_FailedReason_A; 

      L     DIB [AR2,P#6.0]; 
      T     #t_Dir_A.t_FailedReason_B; 

      L     P##i_ANY_Direction_B; 
      LAR1  ; 

      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     DIW [AR2,P#0.0]; 
      T     #t_Dir_B.t_Destination; 

      L     DIB [AR2,P#2.0]; 
      T     #t_Dir_B.t_Preference_A; 

      L     DIB [AR2,P#3.0]; 
      T     #t_Dir_B.t_Preference_B; 

      L     DIB [AR2,P#4.0]; 
      T     #t_Dir_B.t_DecisionLvl; 

      L     DIB [AR2,P#5.0]; 
      T     #t_Dir_B.t_FailedReason_A; 

      L     DIB [AR2,P#6.0]; 
      T     #t_Dir_B.t_FailedReason_B; 

      L     0; 
      T     #t_Direction; 

      L     DINO; 
      T     #t_DINO; 

//Check if there is already a preferred dir
      L     P##i_ANY_Preferred_Dir; 
      LAR1  ; 

      OPN   DI [#t_DINO]; 
      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      A(    ; 
      L     DIB [AR2,P#2.0]; 
      L     0; 
      >I    ; 
      )     ; 
      A(    ; 
      L     DIB [AR2,P#5.0]; 
      L     0; 
      ==I   ; 
      )     ; 
      BEC   ; 
//End block Dir A is succes

      A(    ; 
      L     DIB [AR2,P#3.0]; 
      L     0; 
      >I    ; 
      )     ; 
      A(    ; 
      L     DIB [AR2,P#6.0]; 
      L     0; 
      ==I   ; 
      )     ; 
      BEC   ; 
//End block Dir B is succes

NETWORK
TITLE =BA: Check if preffered 1 is active and determine direction

      A(    ; 
      L     #t_Dir_A.t_Preference_A; 
      L     1; 
      ==I   ; 
      )     ; 
      A(    ; 
      L     #t_Dir_A.t_FailedReason_A; 
      L     0; 
      ==I   ; 
      )     ; 
      JCN   BA01; 

      L     1; 
      T     #t_Direction; 

      JU    BA99; 

BA01: A(    ; 
      L     #t_Dir_B.t_Preference_B; 
      L     1; 
      ==I   ; 
      )     ; 
      A(    ; 
      L     #t_Dir_B.t_FailedReason_B; 
      L     0; 
      ==I   ; 
      )     ; 
      JCN   BA02; 

      L     2; 
      T     #t_Direction; 
      JU    BA99; 

BA02: A(    ; 
      L     #t_Dir_A.t_Preference_A; 
      L     2; 
      ==I   ; 
      )     ; 
      A(    ; 
      L     #t_Dir_A.t_FailedReason_A; 
      L     0; 
      ==I   ; 
      )     ; 
      JCN   BA03; 

      L     1; 
      T     #t_Direction; 

      JU    BA99; 

BA03: A(    ; 
      L     #t_Dir_B.t_Preference_B; 
      L     2; 
      ==I   ; 
      )     ; 
      A(    ; 
      L     #t_Dir_B.t_FailedReason_B; 
      L     0; 
      ==I   ; 
      )     ; 
      JCN   BA99; 

      L     2; 
      T     #t_Direction; 
      JU    BA99; 

//End block if no direction is set

BA99: NOP   0; 
NETWORK
TITLE =CA: Write data to preferred direction

      L     P##i_ANY_Preferred_Dir; 
      LAR1  ; 

      OPN   DI [#t_DINO]; 
      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     #t_Direction; 
      L     0; 
      ==I   ; 
      JCN   CA01; 

      L     #t_Dir_A.t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_Dir_B.t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 
      L     #t_Direction; 
      T     DIB [AR2,P#7.0]; 

      JU    CA99; 

CA01: L     #t_Direction; 
      L     1; 
      ==I   ; 
      JCN   CA02; 

      L     #t_Dir_A.t_Destination; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Dir_A.t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Dir_A.t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_Dir_A.t_DecisionLvl; 
      T     DIB [AR2,P#4.0]; 
      L     #t_Dir_A.t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_Dir_A.t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 
      L     #t_Direction; 
      T     DIB [AR2,P#7.0]; 

      JU    CA99; 

CA02: L     #t_Direction; 
      L     2; 
      JCN   CA99; 

      L     #t_Dir_B.t_Destination; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Dir_B.t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Dir_B.t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_Dir_B.t_DecisionLvl; 
      T     DIB [AR2,P#4.0]; 
      L     #t_Dir_B.t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_Dir_B.t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 
      L     #t_Direction; 
      T     DIB [AR2,P#7.0]; 

CA99: NOP   0; 
END_FUNCTION

