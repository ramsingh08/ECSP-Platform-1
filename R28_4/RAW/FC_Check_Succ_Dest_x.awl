FUNCTION "FC_Check_Succ_Dest_x" : VOID
TITLE =%version: 0.02 % CN: 33
//Function:
//Retreive data for instance DB and then check the succes direction
//
//This version  %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:33:06 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.02
//15797: Keyword release added                          nlwwij   26-09-13   0.02
//Initial Revision                                      nlHdL    02-04-13   0.01
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_ANY_Succes_Dir : ANY ;	//ANY pointer to the Decision messages
END_VAR
VAR_TEMP
  t_Destination : INT ;	//Destination
  t_Preference_A : BYTE ;	//Preference for Direction A
  t_Preference_B : BYTE ;	//Preference for Direction B
  t_DecisionLvl : BYTE ;	//Decision Level	
  t_FailedReason_A : BYTE ;	//Failed Reason for Direction A	
  t_FailedReason_B : BYTE ;	//Failed Reason for Direction B
  t_Direction : BYTE ;	//Direction selected
  t_DINO : INT ;	//DB number of instance	
END_VAR
BEGIN
NETWORK
TITLE =AA: Retreive data of destination x from FB

      L     P##i_ANY_Succes_Dir; 
      LAR1  ; 

      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     DIW [AR2,P#0.0]; 
      T     #t_Destination; 

      L     DIB [AR2,P#2.0]; 
      T     #t_Preference_A; 

      L     DIB [AR2,P#3.0]; 
      T     #t_Preference_B; 

      L     DIB [AR2,P#4.0]; 
      T     #t_DecisionLvl; 

      L     DIB [AR2,P#5.0]; 
      T     #t_FailedReason_A; 

      L     DIB [AR2,P#6.0]; 
      T     #t_FailedReason_B; 

      L     DIB [AR2,P#7.0]; 
      T     #t_Direction; 

      L     DINO; 
      T     #t_DINO; 

NETWORK
TITLE =BA: Check if preffered A is active and determine direction

      L     #t_Direction; 
      L     1; 
      ==I   ; 
      JCN   BA99; 
//Check direction A

      L     #t_FailedReason_A; 
      L     0; 
      ==I   ; 
      JC    BA99; 

//If No failed then end

      L     0; 
      T     #t_Direction; 

BA99: NOP   0; 
NETWORK
TITLE =CA: Check if preffered 2 is active and determine direction

      L     #t_Direction; 
      L     2; 
      ==I   ; 
      JCN   CA99; 
//Check direction B

      L     #t_FailedReason_B; 
      L     0; 
      ==I   ; 
      JC    CA99; 

//If No failed then end

      L     0; 
      T     #t_Direction; 

CA99: NOP   0; 
NETWORK
TITLE =DA: If no direction check Dir A and B

      L     #t_Direction; 
      L     0; 
      ==I   ; 
      JCN   DA99; 
//IF direction = 0 determine direction

      L     #t_Preference_A; 
      L     1; 
      ==I   ; 
      JCN   DA01; 

      L     #t_FailedReason_A; 
      L     0; 
      ==I   ; 
      JCN   DA01; 

//Set to direction A
      L     1; 
      T     #t_Direction; 
      JU    DA99; 

DA01: L     #t_Preference_B; 
      L     1; 
      ==I   ; 
      JCN   DA02; 

      L     #t_FailedReason_B; 
      L     0; 
      ==I   ; 
      JCN   DA02; 

//Set to direction B
      L     2; 
      T     #t_Direction; 
      JU    DA99; 

DA02: L     #t_Preference_A; 
      L     2; 
      ==I   ; 
      JCN   DA03; 

      L     #t_FailedReason_A; 
      L     0; 
      ==I   ; 
      JCN   DA03; 

//Set to direction A
      L     1; 
      T     #t_Direction; 
      JU    DA99; 

DA03: L     #t_Preference_B; 
      L     2; 
      ==I   ; 
      JCN   DA99; 

      L     #t_FailedReason_B; 
      L     0; 
      ==I   ; 
      JCN   DA99; 

//Set to direction B
      L     2; 
      T     #t_Direction; 
      JU    DA99; 

DA99: NOP   0; 
NETWORK
TITLE =DA: Write data to preferred direction

      L     P##i_ANY_Succes_Dir; 
      LAR1  ; 

      OPN   DI [#t_DINO]; 
      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     #t_Destination; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 
      L     #t_Direction; 
      T     DIB [AR2,P#7.0]; 

END_FUNCTION

