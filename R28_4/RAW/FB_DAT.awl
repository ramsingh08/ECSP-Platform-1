FUNCTION_BLOCK "FB_DAT"
TITLE =%version: 0.04 % CN: 42 
//Function:
//Collect all HLC data
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:43:28 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.04
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.04
//15425: Added input No messeges SAC or BPI             nlHdL    04-09-13   0.03
//CR11399 Comment change only                           nlHdL    11-09-12   0.02 
//Init version                                          nlRWwi   29-09-10   0.01 
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_ID_Section : DWORD ;	//ID of Section ID Record HEX AAAA - ZZSS.
  i_DB_Event : BLOCK_DB ;	//Data Structure: Event DB
  i_PLC_FIFO_DB : BLOCK_DB ;	//Data structure: PLC FIFO DB
  i_PLC_Log_FIFO_DB : BLOCK_DB ;	//Data structure: PLC FIFO Log DB
  i_Degraded_Mode : BOOL ;	//CMD: The system is running in degraded mode
  i_CFG_BPI_Log_Level : INT ;	//CFG: [0..10] Log level to BPI (0 = no logging)
  i_CFG_PLC_Log_Level : INT ;	//CFG: [0..10] Log level to BPI (0 = no logging)
  i_CFG_PLC_Log_SAC_Mesgs : BOOL ;	//CFG: Log SAC messages (If TRUE = Messages are logged)
  i_CFG_PLC_LOG_BPI_Stat : BOOL ;	//CFG: Log BPI statisticals (If TRUE = Messages are logged)
  i_CFG_PLC_No_Msg_To_SAC : BOOL ;	//CFG: Send No messages to SAC (If TRUE = No Messages are send to SAC)
  i_CFG_PLC_No_Msg_To_BPI : BOOL ;	//CFG: Send No messages to BPI (If TRUE = No Messages are send to BPI)
END_VAR
VAR
  s_FB_DAT_SAC : "FB_DAT_SAC";	//Block call static FB_DAT_SAC
  s_FB_DAT_BPI_Stat : "FB_DAT_BPI_Stats";	//Block call static FB_DAT_BPI_Stats
  s_FB_DAT_BPI_Log : "FB_DAT_BPI_Log";	//Block call static FB_DAT_BPI_Log
  s_FB_DAT_PLC_Log : "FB_DAT_PLC_Log";	//Block call static FB_DAT_PLC_Log
END_VAR
BEGIN
NETWORK
TITLE =AA: SAC DATA

      NOP   0; // Hold comments

      AN    #i_CFG_PLC_No_Msg_To_SAC; // If TRUE = No Messages are send to SAC
      JCN   AA99; 

      CALL #s_FB_DAT_SAC (
           i_ID                     := #i_ID_Section,
           i_Local_Buffer           := #i_DB_Event,
           i_Degraded_Mode          := #i_Degraded_Mode,
           i_PLC_FIFO_DB            := #i_PLC_FIFO_DB);

AA99: NOP   0; 
NETWORK
TITLE =AB: BPI Statisticals

      NOP   0; // Hold comments

      AN    #i_CFG_PLC_No_Msg_To_BPI; // If TRUE = No Messages are send to BPI
      JCN   AB99; 

      CALL #s_FB_DAT_BPI_Stat (
           i_ID                     := #i_ID_Section,
           i_Local_Buffer           := #i_DB_Event,
           i_PLC_FIFO_DB            := #i_PLC_FIFO_DB);

AB99: NOP   0; 
NETWORK
TITLE =AC: BPI Logging

      CALL #s_FB_DAT_BPI_Log (
           i_ID                     := #i_ID_Section,
           i_Local_Buffer           := #i_DB_Event,
           i_PLC_FIFO_DB            := #i_PLC_FIFO_DB,
           i_CFG_Log_Level          := #i_CFG_BPI_Log_Level);

NETWORK
TITLE =AD: PLC Logging

      CALL #s_FB_DAT_PLC_Log (
           i_ID                     := #i_ID_Section,
           i_Local_Buffer           := #i_DB_Event,
           i_PLC_Log_FIFO_DB        := #i_PLC_Log_FIFO_DB,
           i_CFG_Log_Level          := #i_CFG_PLC_Log_Level,
           i_CFG_Log_SAC_Messages   := #i_CFG_PLC_Log_SAC_Mesgs,
           i_CFG_LOG_BPI_Statistics := #i_CFG_PLC_LOG_BPI_Stat);

END_FUNCTION_BLOCK

