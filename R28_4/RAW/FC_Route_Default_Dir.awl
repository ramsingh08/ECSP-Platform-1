FUNCTION "FC_Route_Default_Dir" : VOID
TITLE =%version: 0.05 % CN: 33
//Function:
//Check if decision point is in default mode (No Routing table)
//Write preffered and alternative direction in the destination
//
//This version  %created_by: nlhdl %
//              %date_created: maandag 6 juli 2015 10:33:48 %
//              %release: VI_CONV/RA_Dev_Oakham %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.05
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.05
//PR11918: Adopted Decision point update with Dir A & B nlHdL    02-04-13   0.04
//CR19512 Update sources according S7 Manager           nlHdL    08-03-13   0.03
//PR11727 Add synergy key words 'created_by' and date   nlHdL    14-11-12   0.02
//Initial Revision                                      nlHdL    03-10-11   0.01
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_CFG_Dir_A_Preference : INT ;	//CFG: [Dir] Preference for Direction A (0 = No Direction 1 = Preferred)
  i_CFG_Dir_B_Preference : INT ;	//CFG: [Dir] Preference for Direction A (0 = No Direction 1 = Preferred)
  i_ANY_Direction_A : ANY ;	//ANY pointer to the Direction A messages
  i_ANY_Direction_B : ANY ;	//ANY pointer to the Direction B messages
  i_DB_Event_Num : INT ;	//Number of DB event
  i_Rep_Route : INT ;	//Replay Route message for tracking report
END_VAR
VAR_TEMP
  t_Preference_A : BYTE ;	//Preference for Direction A
  t_Preference_B : BYTE ;	//Preference for Direction B
  t_DecisionLvl : BYTE ;	//Decision Level	
  t_FailedReason_A : BYTE ;	//Failed Reason for Direction A	
  t_FailedReason_B : BYTE ;	//Failed Reason for Direction B
  t_Failed1 : BYTE ;	//Failed Direction A
END_VAR
BEGIN
NETWORK
TITLE =AA: Check Default Routing A

      L     0; 
      T     #t_DecisionLvl; 
      T     #t_FailedReason_A; 
      T     #t_FailedReason_B; 
      T     #t_Failed1; 

      L     0; 
      L     #i_CFG_Dir_A_Preference; 
      ==I   ; 
      JCN   AA01; 

      L     1; 
      T     #t_DecisionLvl; 

      L     1; 
      T     #t_FailedReason_A; 
      T     #t_Failed1; 

      JU    AA02; 
//If no default is set FailedReason = 1 ("NO ROUTING DEFAULT")

AA01: L     #i_CFG_Dir_A_Preference; 
      L     0; 
      >I    ; 
      JCN   AA02; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_B_Preference; 
      ==I   ; 
      JCN   AA02; 

      L     1; 
      T     #t_FailedReason_B; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

AA02: NOP   0; 
      L     #i_CFG_Dir_A_Preference; 
      T     #t_Preference_A; 

      L     #i_CFG_Dir_B_Preference; 
      T     #t_Preference_B; 

      L     P##i_ANY_Direction_A; 
      LAR1  ; 

      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     0; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 

      NOP   0; 
NETWORK
TITLE =BA: Check Default Routing B

      L     0; 
      T     #t_DecisionLvl; 
      T     #t_FailedReason_A; 
      T     #t_FailedReason_B; 

      L     0; 
      L     #i_CFG_Dir_B_Preference; 
      ==I   ; 
      JCN   BA01; 

      L     1; 
      T     #t_DecisionLvl; 

      L     1; 
      T     #t_FailedReason_B; 
//If no default is set FailedReason = 1 ("NO ROUTING DEFAULT")

      L     #t_Failed1; 
      L     1; 
      ==I   ; 
      JCN   BA02; 
//If A and B is failed then report Failed direction

      CALL "FC_Exec_Failed_Dir" (
           i_Rep_Route              := #i_Rep_Route,
           i_Failed_Direction       := 0,
           i_Failed_Reason          := 1,
           i_Failed_Destination     := 0,
           i_Failed_Decisionlvl     := 1,// Routing default
           i_DB_Event_Num           := #i_DB_Event_Num);

      JU    BA02; 

BA01: L     #i_CFG_Dir_B_Preference; 
      L     0; 
      >I    ; 
      JCN   BA02; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_A_Preference; 
      ==I   ; 
      JCN   BA02; 

      L     1; 
      T     #t_FailedReason_A; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

BA02: NOP   0; 
      L     #i_CFG_Dir_A_Preference; 
      T     #t_Preference_A; 

      L     #i_CFG_Dir_B_Preference; 
      T     #t_Preference_B; 

      L     P##i_ANY_Direction_B; 
      LAR1  ; 

      L     W [AR1,P#8.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     0; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#6.0]; 

END_FUNCTION

